grammar org.xtext.example.mydsl.MyDsl with org.eclipse.xtext.common.Terminals

generate myDsl "http://www.xtext.org/example/mydsl/MyDsl"

Graph:
	((components += Component) | (facets += Facet) | (instances += Instance) | (imports += Import))*
;

terminal SL_COMMENT: 
	'#' !('\n' | '\r')* '\r'? '\n'
;

terminal ID: 
	('a'..'z'|'A'..'Z'|'_'|'-') ('a'..'z'|'A'..'Z'|'_'|'-'|'0'..'9')*
;

GRAPH_FILE:
	ID '.graph'
;

IP:
	INT '.' INT '.' INT '.' INT
;

VARIABLEVALUE:
	INT | STRING | IP
;

Component:
	(name = ID) '{' (properties = ComponentProperties) '}'
;

Facet:
	'facet' (name = ID) '{' (properties = FacetProperties) '}'
;

Instance:
	'instance of' (name = ID) '{' (properties = InstanceProperties) '}'
;

Import:
	'import' (name = GRAPH_FILE) ';'
;

ComponentProperties:
	(optionalProperties += OptionalComponentProperty)*
	(installerProperty = InstallerProperty)
	(optionalProperties += OptionalComponentProperty)*
;

FacetProperties:
	(properties += FacetProperty)* 
;

InstanceProperties:
	(optionalProperties += OptionalInstanceProperty)*
	(nameProperty = NameProperty)
	(optionalProperties += OptionalInstanceProperty)*
;

FacetProperty:
	(childrenProperty = ChildrenProperty) | 
	(exportsProperty = ExportsProperty)
;

OptionalComponentProperty:
	(childrenProperty = ChildrenProperty) |
	(exportsProperty = ExportsProperty) |
	(importsProperty = ImportsProperty) |
	(facetsProperty = FacetsProperty) |
	(extendsProperty = ExtendsProperty)
;

OptionalInstanceProperty:
	(countProperty = CountProperty) |
	(channelsProperty = ChannelsProperty) |
	(instanceDataProperty = InstanceDataProperty) |
	(instanceStateProperty = InstanceStateProperty) |
	(compositionProperty = CompositionProperty) |
	(exportedVariablesProperty = ExportedVariablesProperty)
;

ExportedVariablesProperty:
	(name = ID) ':' (value = VARIABLEVALUE) ';' 	
;

InstallerProperty:
	'installer:' (name = ID) ';'
;

ChildrenProperty:
	'children:' (name += ID) (',' (name += ID))* ';'
;

ExportsProperty:
	'exports:' (exportsVariables += ExportsVariable) (',' (exportsVariables += ExportsVariable))* ';'
;

ExportsVariable:
	(name = 'ip') |
	((name = ID) '=' (value = VARIABLEVALUE)) 
;

ImportsProperty:
	'imports:' (importsVariables += ImportsVariable) (',' (importsVariables += ImportsVariable))* ';'
;

ImportsVariable:
	(isExternal ?= 'external')? (componentName = ID) '.' (componentProperty = ID | 'ip') ((isOptional ?= '(optional)'))?
;

FacetsProperty:
	'facets:' (facetsNames += ID) (',' (facetsNames += ID))* ';'
;

ExtendsProperty:
	'extends:' (extendsNames += ID) (',' (extendsNames += ID))* ';'
;

NameProperty:
	'name:' (name = ID) ';'
;

CountProperty:
	'count:' (name = INT) ';'	
;

ChannelsProperty:
	'channels:' (name = STRING) ';'
;

InstanceDataProperty:
	'instance-data:' (name = STRING) ';'
;

InstanceStateProperty:
	'instance-state:' (name = STRING) ';'	
;

CompositionProperty:
	Instance
;

